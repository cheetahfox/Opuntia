opengt
 set com 115200n81
 set comecho off
 set senddelay 0.02
 waitquiet 0.2 0.2

 send "AT\^CURC=0^m"
 waitfor 5 "OK","ERROR","\^RSSI"

 let $c="AT+CSQ^m"
 let $r="+CSQ"
 gosub readatcmd

 let $c="ATE1; +CGMI^m"
 gosub readatcmdnr
 let $v=$s

 if $toupper($mid($v,0,3)) = "ZTE" goto zte
 if $toupper($mid($v,0,3)) = "OPT" goto option
 if $toupper($mid($v,0,3)) = "SIE" goto sierra
 if $toupper($mid($v,0,3)) = "NOV" goto novatel
 if $toupper($mid($v,0,3)) = "HUA" goto huawei
 if $toupper($mid($v,0,3)) = "VOD" goto vodafone
 if $toupper($mid($v,0,3)) = "NOK" goto vodafone
 goto generic

:vodafone
 goto huawei

:novatel
 let $c="AT$CNTI=0^m"
 let $r="$CNTI"
 gosub readatcmd
 goto next

:sierra
 let $c="AT*CNTI=0^m"
 let $r="*CNTI"
 gosub readatcmd

 let $c="AT!SELRAT?^m"
 let $r="!SELRAT"
 gosub readatcmd

 let $c="AT+ECIO?^m"
 let $r="Car0"
 gosub readatcmd2

 let $c="AT+RSCP?^m"
 let $r="Car0"
 gosub readatcmd2

 let $c="AT!GSTATUS?^m"
 let $r="RX"
 gosub readatcmd3

 let $c="AT!GSTATUS?^m"
 let $r="RSSI"
 gosub readatcmd3

 goto next

:option
 let $c="AT_OCTI?^m"
 let $r="_OCTI"
 gosub readatcmd

 let $c="AT_OWCTI?^m"
 let $r="_OWCTI"
 gosub readatcmd
 goto next

:zte
 let $c="AT+COPS?^m"
 let $r="+COPS"
 gosub readatcmd

 let $c="AT+ZPAS?^m"
 let $r="+ZPAS"
 gosub readatcmd

 let $c="AT+ZRSSI^m"
 let $r="+ZRSSI"
 gosub readatcmd

 let $c="AT+ZRSSI?^m"
 let $r="+ZRSSI"
 gosub readatcmd

 let $c="AT+ZSNT?^m"
 let $r="+ZSNT"
 gosub readatcmd

 goto next1

:huawei
 let $c="AT\^CSNR?^m"
 let $r="\^CSNR"
 gosub readatcmd

 let $c="AT\^SYSINFOEX^m"
 let $r="\^SYSINFOEX"
 gosub readatcmd

 let $c="AT\^SYSCFGEX?^m"
 let $r="\^SYSCFGEX"
 gosub readatcmd

 let $c="AT\^SYSCFG?^m"
 let $r="\^SYSCFG"
 gosub readatcmd

:generic
 let $c="AT\^SYSINFO^m"
 let $r="\^SYSINFO"
 gosub readatcmd

 let $c="AT\^LTERSRP?^m"
 let $r="\^LTERSRP"
 gosub readatcmd

:next
 let $c="AT+COPS?^m"
 let $r="+COPS"
 gosub readatcmd

:next1
 let $c="AT+CREG=2^m"
 let $r="OK"
 gosub readatcmd
 let $c="AT+CREG?^m"
 let $r="+CREG"
 gosub readatcmd

 let $c="AT+CGREG=2^m"
 let $r="OK"
 gosub readatcmd
 let $c="AT+CGREG?^m"
 let $r="+CGREG"
 gosub readatcmd

 let $c="AT+CGEQNEG=1^m"
 let $r="+CGEQNEG"
 gosub readatcmd

 let $c="ATI^m"
 let $r="Manufacturer:"
 gosub readatcmd2

 let $c="ATE1; +CGMM^m"
 gosub readatcmdnr
 print "\nDEVICE:",$v,"\n"

 exit 0

:readatcmd
 let i=5
 send $c
:loop
 get 1 "^m" $s
 let l=len($r)
 if len($s) < l goto loop1
 if $mid($s,1,l) <> $r goto loop1
 print $s
 return

:loop1
 if len($s) < 2 goto loop2
 if $mid($s,1,2) = "ER" return
 if $mid($s,1,2) = "CO" return
:loop2
print $s
 if i = 0 return
 let i=i-1
 sleep 0.25
 goto loop

:readatcmd2
 let i=5
 send $c
:loop6
 get 1 "^m" $s
 let l=len($r)
 if len($s) < l goto loop7
 if $mid($s,1,l) <> $r goto loop7
 print $s
 get 1 "^m" $s
 print $s
 return

:loop7
 if len($s) < 2 goto loop8
 if $mid($s,1,3) = "ERR" return
 if $mid($s,1,2) = "CO" return
:loop8
 if i = 0 return
 let i=i-1
 sleep 0.25
 goto loop6

:readatcmd3
 let i=15
 send $c
:loop9
 get 1 "^m" $s
 let l=len($r)
 if len($s) < l goto loop10
 if $mid($s,1,l) <> $r goto loop10
 print $s
 get 1 "^m" $s
 print $s
 get 1 "^m" $s
 print $s
 get 1 "^m" $s
 print $s
 return

:loop10
 if len($s) < 2 goto loop11
 if $mid($s,1,3) = "ERR" return
 if $mid($s,1,2) = "CO" return
:loop11
 if i = 0 return
 let i=i-1
 sleep 0.25
 goto loop9

:readatcmdnr
 let i=10
 send $c
:loop3
 get 1 "^m" $s
 if len($s) < 2 goto loop5
 if $mid($s,1,2) = "ER" goto loop4
 if $mid($s,1,2) = "OK" goto loop5
 if $mid($s,1,2) = "AT" goto loop5
 if $mid($s,1,2) = "TE" goto loop5
 if $mid($s,1,1) = "+"  goto loop5
 if $mid($s,1,1) = "\^"  goto loop5
 let l=len($s)
 let $s=$mid($s,1,l)
 return

:loop4
 let $s=""
 return

:loop5
 if i = 0 return
 let i=i-1
 sleep 0.25
 goto loop3
